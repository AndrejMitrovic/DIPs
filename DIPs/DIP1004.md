# Inherited Constructors

| Section         | Value                                                      |
|-----------------|------------------------------------------------------------|
| DIP:            | 1004                                                       |
| Author:         | Andrej MitroviÄ‡                                            |
| Implementation: |                                                            |
| Status:         | Draft                                                      |

## Table of Contents

* [Abstract](#abstract)
* [Rationale](#rationale)
* [Description](#description)
* [Proposed features](#proposed-features)
  * [Add implicit constructor inheritance for derived classes with no constructors](#1-add-implicit-constructor-inheritance-for-derived-classes-with-no-constructors)
  * [Add language support for inheriting base class constructors for derived classes with constructors](#2-add-language-support-for-inheriting-base-class-constructors-for-derived-classes-with-constructors)
  * [Syntax Proposals](#syntax-proposals)
  * [Selective Disabling of Inherited Constructors](#selective-disabling-of-inherited-constructors)
* [Existing Library Workarounds](#existing-workarounds)
* [Real-world examples](#real-world-examples)
* [Breaking Changes](#breaking-changes)
* [Links](#links--previous-discussions)
* [Copyright &amp; License](#copyright--license)

## Abstract

A derived class is currently instantiable by client-code using a base class
constructor only if that constructor is a default constructor.

If the implementor of the derived class wants to allow construction of the
derived class by using any of the base class constructors then this support
must be added manually by writing forwarding constructors.

This DIP attempts to alleviate the problem by introducing two features:

- 1. [Add implicit constructor inheritance for derived classes with no constructors](#1-add-implicit-constructor-inheritance-for-derived-classes-with-no-constructors)
- 2. [Add language support for inheriting base class constructors for derived classes with constructors](#2-add-language-support-for-inheriting-base-class-constructors-for-derived-classes-with-constructors)

## Rationale

The process of writing forwarding constructors is tedious and may even be
error-prone. It's possible to use existing language features such as template
mixins to generate forwarding constructors, however there are drawbacks:

- Compile-time reflection on the type may cause forward reference issues while
  mixing-in the constructor code
- Using mixins can potentially increase compilation times

## Description

Currently a derived class is constructible by client code via its base class
constructor only if the derived class does not define any of its own
constructors and additionally only if the base class constructor is a default
constructor.

Example:

```D
class Base
{
    int sum;

    this ( )
    {
        this.sum = 5 + 5;
    }
}

class Derived : Base { }

void main ( )
{
    auto d = new Derived;  // ok. Derived doesn't define custom constructors
    assert(d.sum == 10);
}
```

However, if the base class constructor is a non-default constructor it will be
hidden when instantiating a derived class object:

```D
class Base
{
    int sum;

    this ( int x, int y )
    {
        this.sum = x + y;
    }
}

class Derived : Base
{
    this ( )
    {
        super(5, 5);
    }
}

void main ( )
{
    auto d = new Derived(10, 10);  // error
}
```

The rationale for this current limitation: If the `Derived` object was
constructible by calling only the `Base` class constructor then the `Derived`
object itself could be left in an *unexpected* state. This is because the
`Derived` constructors could be skipped and never called and potentially
leaving `Derived`'s own internal fields in an unexpected state.

If the author of the `Derived` class wants to allow construction via the base
class constructor then this support must be added explicitly by writing
forwarding constructors:

```D
class Base
{
    int sum;

    this ( int x, int y )
    {
        this.sum = x + y;
    }
}

class Derived : Base
{
    this ( )
    {
        super(5, 5);
    }

    // forwarding constructor
    this ( int x, int y )
    {
        super(x, y);
    }
}

void main ( )
{
    auto d1 = new Derived();  // ok
    assert(d1.sum == 10);

    auto d2 = new Derived(10, 10);  // ok
    assert(d2.sum == 20);
}
```

Note that the limitation is presesnt even if the derived class does not defined
any of its own constructors:

```D
class Base
{
    int sum;

    this ( int x, int y )
    {
        this.sum = x + y;
    }
}

class Derived : Base
{
}

void main ( )
{
    auto d = new Derived(10, 10);  // error
}
```

## Proposed features

### 1. Add implicit constructor inheritance for derived classes with no constructors

If a derived class does not define any constructors of its own then that class
will automatically inherit all base class constructors.

Example:

```D
class ParseException : Exception
{
    // inherits all of Exception's constructors
}

void parseArgs ( string[] args )
{
    if (args < 2)
    {
        throw new ParseException("Expected at least one argument");
    }
}

int main ( string[] args )
{
    try
    {
        parseArgs(args);

        return 0;
    }
    catch (ParseException ex)
    {
        stderr.writefln("Parsing error: %s", ex.msg);
    }
    catch (Exception ex)
    {
        stderr.writefln("Unexpected error: %s", ex.msg);
    }

    return 1;
}
```

**Rationale**: If a derived class does not define any of its own constructors
then there is no danger of leaving the object in an unexpected state.

Side-note: Forwarding constructors are most prominent in exception class
hierarchies.

### 2. Add language support for inheriting base class constructors for derived classes with constructors

If the author of the derived class defines its own set of constructors but also
wants to allow construction via the base class'es constructors then the new
constructor inheritance syntax can be used for this purpose:

```D
enum ErrorCode
{
    FileNotFound
}

class FileException : Exception
{
    /// introduce overload which takes an error code
    this ( ErrorCode error_code, string file = __FILE__, string line = __LINE__ )
    {
        super(error_code.to!string, file, line);
    }

    /// inherit base class constructors
    alias super.this this;
}

void main ( string[] args )
{
    // using Exception's constructor
    throw new FileException("Something went wrong");

    // using FileException's constructor
    throw new FileException(ErrorCode.FileNotFound);
}
```

This feature is similar to D's existing feature for re-introducing base class
methods to allow overloading of a base class method:

```D
class Base
{
    int sum ( int x, int y ) { return x + y; }
}

class Derived : Base
{
    // re-introduces base class method to allow overloading
    alias super.sum sum;

    // overload of the base class sum method
    float sum ( float x, float y ) { return x + y; }
}

void main ( string[] args )
{
    auto d = new Derived;
    d.sum(int(1), int(2));
    d.sum(float(1), float(2));
}
```

#### Syntax proposals

There are at least several possible syntaxes which can be used for this feature:

```D
class FileException : Exception
{
    /// option A: simple syntax which references the ctor via the keyword 'this'
    alias super.this this;

    /// option B: using parentheses to make it explicit we're refering to ctors
    alias super.this() this();

    /// option C: the most simplistic syntax, although it may be confused with
    /// the syntax for subtyping
    alias super this;
```

#### Selective disabling of inherited constructors

Since the proposed feature for explicitly inheriting base class constructors
actually inherits **all** base-class constructors, there may be a need to allow
disabling inheriting specific constructors. For example:

```D
class Base
{
    this ( int x, int y ) { }
    this ( string x, string y ) { }
}

class Derived : Base
{
    alias super.this this;

    @disable this(int, int);  // disable this overload of the inherited constructor
}

void main ( )
{
    new Derived("foo", "bar");  // ok
    new Derived(1, 2);  // error: constructor is disabled
}
```

## Existing workarounds

An example library solution for inheriting constructors via mixins is provided
[here](https://gist.github.com/AndrejMitrovic/72a08aa2c078767ea4c35eb1b0560c8d).

There are a number of drawbacks with the above implementation:

- It relies on string mixins to generate the code, which can slow down
  compilation speed.
- It does not handle default values.
- It does not have the ability to selectively disable inheriting specific
  constructors. The API could be expanded to support this, however it would
  make the API awkward to use.

## Real-world examples

The most common example in library code is an exception class hierarchy.
Typically each new inherited class has to define forwading constructors in
order to make it usable.

An example of an exception class hierarchy can be found
[here](https://github.com/sociomantic-tsunami/ocean/blob/6500d67e630de1d05adc510e1572bee26fe3985c/src/ocean/core/Exception_tango.d).

With the implicit inheritance feature the module would be simplified and might
look like
[this](https://gist.github.com/AndrejMitrovic/752a57f51f595b471bc6595a4ec58112).

If the constructor in the common base class of all these exception types
changes its number of parameters or if the base class adds another overload of
the contructor then the derived classes do not have to be edited.

The D standard library also defines a helper mixin to make it easier to define
your own exception class
[here](https://github.com/dlang/phobos/blob/bf61ad682f3f9c35a16c79180941ffd902ab9758/std/exception.d#L2159),
showing that there may be a benefit to adding support for inherited
constructors on the language level.

## Breaking changes

The vast majority of code should not be affected by the changes in this proposal.

Code which uses introspection to check whether a class is instantiable with a
list of arguments would be the most affected by this change.  For example, an
`is(typeof( new Derived(...) ))` check may currently be false, but would change
to true if constructors were implicitly inherited as per [feature
#1](#1-add-implicit-constructor-inheritance-for-derived-classes-with-no-constructors)

## Links & Previous discussions

* [Issue 9066: Add constructor inheritance feature](https://issues.dlang.org/show_bug.cgi?id=9066)
* [Previous discussion about constructor inheritance in D](http://forum.dlang.org/post/f3pisd$4vi$3@digitalmars.com)
* [C++11: What is constructor inheritance?](http://stackoverflow.com/a/9979249/279684)
* [C++11: Draft 3337 - Page 269 - Constructor Inheritance (PDF warning)](http://www.open-std.org/jtc1/sc22/wg21/docs/papers/2012/n3337.pdf)
* [C++11: Object construction improvements](https://en.wikipedia.org/wiki/C%2B%2B11#Object_construction_improvement)

## Copyright & License

Copyright (c) 2016 by the D Language Foundation

Licensed under [Creative Commons Zero
1.0](https://creativecommons.org/publicdomain/zero/1.0/legalcode.txt)
